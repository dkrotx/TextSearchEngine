version '0.1'

apply plugin: 'java'
apply plugin: 'application'

mainClassName=""
sourceCompatibility = 8

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.0'
    }
}

dependencies {
    compile group: 'de.l3s.boilerpipe', name: 'boilerpipe', version: '1.1.0'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile group: 'gnu.getopt', name: 'java-getopt', version: '1.0.13'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '2.6.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

apply plugin: 'com.google.protobuf'

protobuf {
    generateProtoTasks {
        // all() returns the collection of all protoc tasks
        all().each { task ->
            // Here you can configure the task
            task.builtins {
                // In Java projects, the "java" output is added automatically.
                // You only need it if you want it in an Android project or want to add
                // options.
                // DO NOT omit the braces if you want this builtin to be added.
                java {}

            }
        }

        // In addition to all(), you may get the task collection by various
        // criteria:

        // (Java only) returns tasks for a sourceSet
        ofSourceSet('main')

    }
    generatedFilesBaseDir = "$projectDir/src/generated"
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:2.6.1'
    }
}

sourceSets {
    main {
        java {
            srcDir "src/generated/main/java"
        }
    }
}

clean {
    delete "src/generated"
}


// Creates scripts for entry points
// Subproject must apply application plugin to be able to call this method.
def createScript(project, mainClass, name) {
    project.tasks.create(name: name, type: CreateStartScripts) {
        outputDir       = new File(project.buildDir, 'scripts')
        mainClassName   = mainClass
        applicationName = name
        classpath       = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtime
    }
    project.tasks[name].dependsOn(project.jar)

    project.applicationDistribution.with {
        into("bin") {
            from(project.tasks[name])
            fileMode = 0755
        }
    }
}

// The next two lines disable the tasks for the primary main which by default
// generates a script with a name matching the project name.
// You can leave them enabled but if so you'll need to define mainClassName
// And you'll be creating your application scripts two different ways which
// could lead to confusion
startScripts.enabled = false
run.enabled = false

// Call this for each Main class you want to expose with an app script
createScript(project, 'org.bytesoft.tsengine.demo.HtmlIndexerDemo', 'index.sh')
createScript(project, 'org.bytesoft.tsengine.demo.DictionaryMaker', 'make_dict.sh')
createScript(project, 'org.bytesoft.tsengine.demo.OptimizerDemo',   'optimize.sh')
createScript(project, 'org.bytesoft.tsengine.demo.SearcherDemo',    'search.sh')
